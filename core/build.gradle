apply plugin: "java-library"
apply plugin: 'org.ajoberstar.grgit'
apply plugin: 'com.github.gmazzo.buildconfig'

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'
compileJava.options.incremental = true

sourceSets.main.java.srcDirs += [ 'src/' ]
sourceSets.main.resources.srcDirs += [ 'resources/' ]

// Add all basisu-wrapper module natives.
fileTree("${project(':basisu-wrapper').projectDir}/libs").matching {
    include '**/*.so'
    include '**/*.dll'
    include '**/*.dylib'
}.each {
    sourceSets.main.resources.srcDirs += it.parent
}

dependencies {
    api project(':basisu-wrapper')

    api "com.badlogicgames.gdx:gdx:$gdxVersion"
//    api "com.badlogicgames.gdx:gdx-freetype:$gdxVersion" // We use locally modified copy of this library.
    api "com.crashinvaders.lml:gdx-lml:$lmlVersion"
    api "com.crashinvaders.lml:gdx-lml-vis:$lmlVersion"
    api "com.crashinvaders.lml:gdx-autumn:$lmlVersion"
    api "com.crashinvaders.lml:gdx-autumn-mvc:$lmlVersion"
    api "com.kotcrab.vis:vis-ui:$visUiVersion"
    api "commons-io:commons-io:$commonsIoVersion"
    api "com.github.depsypher:pngtastic:$pngtasticVersion"
    api "com.tinify:tinify:$tinifyVersion"
    api "com.github.tommyettinger:anim8-gdx:$anim8Version"

    api files('libs/libimagequant-java.jar')
}

buildConfig {
    useJavaOutput()
    className("CoreBuildConfig")
    packageName("com.crashinvaders.texturepackergui.generated")

    buildConfigField("String", "APP_NAME", "\"$project.name\"")
    buildConfigField("String", "APP_VERSION", "\"$project.version\"")
    buildConfigField("long", "BUILD_TIME", "${System.currentTimeMillis()}L")
    buildConfigField("String", "COMMIT", "\"${grgit.head().abbreviatedId}\"")
}
tasks.compileJava.dependsOn generateBuildConfig